buildscript {
    ext {
        buildToolsVersion = "36.0.0"
        minSdkVersion = 24
        compileSdkVersion = 36
        targetSdkVersion = 36
        ndkVersion = "27.1.12297006"
        kotlinVersion = "2.1.20"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

apply plugin: "com.facebook.react.rootproject"

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url("$rootDir/../../../node_modules/react-native/android") }
    }
}

def workspaceRoot = rootProject.projectDir.parentFile.parentFile.parentFile
def publishDir = project.findProperty("rnPublishDir") ?: "${workspaceRoot}/maven-repo"
def rnDepsVersion = project.findProperty("rnDepsVersion") ?: "1.0.0"
def rnNativeModules = [
    "react-native-gesture-handler",
    "react-native-screens",
    "react-native-safe-area-context",
]

subprojects { subproject ->
    if (rnNativeModules.contains(subproject.name)) {
        subproject.plugins.apply("maven-publish")
        subproject.plugins.withId("com.android.library") {
            subproject.android.publishing {
                singleVariant("release") {
                    withSourcesJar()
                }
            }
        }
        subproject.afterEvaluate {
            subproject.publishing {
                publications {
                    release(MavenPublication) {
                        def aarTask = subproject.tasks.findByName("bundleReleaseAar")
                        if (aarTask == null) {
                            aarTask = subproject.tasks.findByName("assembleRelease") ?: subproject.tasks.findByName("assemble")
                        }
                        artifact(subproject.file("${subproject.buildDir}/outputs/aar/${subproject.name}-release.aar")) {
                            if (aarTask != null) {
                                builtBy aarTask
                            }
                        }
                        groupId = "com.circles.reactnative"
                        artifactId = subproject.name
                        version = rnDepsVersion
                    }
                }
                repositories {
                    maven {
                        name = "localRepo"
                        url = uri(publishDir)
                    }
                }
            }
        }
    }
}
